# Описание задания

В конфигурации задан регистр сведений «Посетители» следующей структуры:
- Измерение: «Посетитель» (справочник «Посетители»).
- Ресурс: «Направление» (перечисление, «Вошел, Вышел»).

Регистр — периодический, независимый. Например:

|    Период   | Посетитель  | Направление |
| ----------- | ----------- | ----------- |
| 23.08 10:42 |      А      |    Вошел    |
| 23.08 10:43 |      Б      |    Вошел    |
| 23.08 10:44 |      В      |    Вошел    |
| 23.08 10:45 |      Г      |    Вошел    |
| 23.08 10:46 |      А      |    Вышел    |
| 23.08 10:47 |      Д      |    Вошел    |
| 23.08 10:48 |      Ж      |    Вошел    |
| 23.08 11:05 |      А      |    Вошел    |
| 23.08 11:10 |      Б      |    Вышел    |
| 23.08 11:20 |      Г      |    Вышел    |
| 23.08 11:50 |      К      |    Вошел    |


Физический смысл регистра: логирование прохода посетителей в некое помещение (например, посещение доклада на партнерском семинаре).

Задача: подсчитать «пиковую нагрузку» на помещение за указанный промежуток времени. Под «пиковой нагрузкой» понимается максимальное количество посетителей, которые находились в помещении одновременно.


---

# Решение в виде запроса

```
ВЫБРАТЬ
	Посетители.Период КАК Период,
	Посетители.Посетитель КАК Посетитель,
	Посетители.Направление КАК Направление,
	ВЫБОР
		КОГДА Посетители.Направление = ЗНАЧЕНИЕ(Перечисление.Направление.Вошел)
			ТОГДА 1
		ИНАЧЕ -1
	КОНЕЦ КАК ЧисловоеНаправление
ПОМЕСТИТЬ втПосетителиСЧисловымПоказателемНаправления
ИЗ
	РегистрСведений.Посетители КАК Посетители
{ГДЕ
	(Посетители.Период >= &НачалоПериода),
	(Посетители.Период <= &КонецПериода)}
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втПосетителиСЧисловымПоказателемНаправления.Период КАК Период,
	втПосетителиСЧисловымПоказателемНаправления.Посетитель КАК Посетитель,
	втПосетителиСЧисловымПоказателемНаправления.Направление КАК Направление,
	втПосетителиСЧисловымПоказателемНаправления.ЧисловоеНаправление КАК ЧисловоеНаправление,
	СУММА(ВЫБОР
			КОГДА втПосетителиСЧисловымПоказателемНаправления2.Направление = ЗНАЧЕНИЕ(Перечисление.Направление.Вошел)
				ТОГДА 1
			ИНАЧЕ -1
		КОНЕЦ) КАК КоличествоТекущихПосетителей
ПОМЕСТИТЬ втКоличествоТекущихПосетителей
ИЗ
	втПосетителиСЧисловымПоказателемНаправления КАК втПосетителиСЧисловымПоказателемНаправления2
		ЛЕВОЕ СОЕДИНЕНИЕ втПосетителиСЧисловымПоказателемНаправления КАК втПосетителиСЧисловымПоказателемНаправления
		ПО (втПосетителиСЧисловымПоказателемНаправления.Период >= втПосетителиСЧисловымПоказателемНаправления2.Период)

СГРУППИРОВАТЬ ПО
	втПосетителиСЧисловымПоказателемНаправления.Период,
	втПосетителиСЧисловымПоказателемНаправления.Посетитель,
	втПосетителиСЧисловымПоказателемНаправления.Направление,
	втПосетителиСЧисловымПоказателемНаправления.ЧисловоеНаправление
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	МАКСИМУМ(втКоличествоТекущихПосетителей.КоличествоТекущихПосетителей) КАК ПиковаяНагрузка
ПОМЕСТИТЬ втПиковаяНагрузка
ИЗ
	втКоличествоТекущихПосетителей КАК втКоличествоТекущихПосетителей
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втКоличествоТекущихПосетителей.Период КАК ВремяПиковойНагрузки,
	втПиковаяНагрузка.ПиковаяНагрузка КАК ПиковаяНагрузка
ИЗ
	втКоличествоТекущихПосетителей КАК втКоличествоТекущихПосетителей
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПиковаяНагрузка КАК втПиковаяНагрузка
		ПО втКоличествоТекущихПосетителей.КоличествоТекущихПосетителей = втПиковаяНагрузка.ПиковаяНагрузка
```
